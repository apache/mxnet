// -*- mode: groovy -*-

// Licensed to the Apache Software Foundation (ASF) under one
// or more contributor license agreements.  See the NOTICE file
// distributed with this work for additional information
// regarding copyright ownership.  The ASF licenses this file
// to you under the Apache License, Version 2.0 (the
// "License"); you may not use this file except in compliance
// with the License.  You may obtain a copy of the License at
//
//   http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing,
// software distributed under the License is distributed on an
// "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
// KIND, either express or implied.  See the License for the
// specific language governing permissions and limitations
// under the License.
//
// Jenkins pipeline
// See documents at https://jenkins.io/doc/book/pipeline/jenkinsfile/

// This job executes a "generic" (CD) release job.
// The pipeline to be executed is defined by the "RELEASE_JOB_TYPE" pipeline parameter.
// This is the path (relative to the CD directory) to a directory containing the following file groovy script
// `jenkins/Jenkins_steps.groovy`. 
// Current examples are: binary_release/static, binary_release/dynamic, python/pypi, runtime_images, etc.
// The groovy script defines a function (get_pipeline(mxnet_variant)) that returns the pipeline definition for 
// the particular mxnet variant (e.g. cpu, mkl, cu90, cu100mkl, etc.).
// This job will execute the pipelines in parallel for the variants defined in the "MXNET_VARIANTS" pipeline parameter.

// timeout of each step in minutes
max_time = 180

pipeline {
  agent {
    label 'restricted-utility'
  }

  parameters {
    // Release parameters
    string(defaultValue: "master", description: 'The incubator-mxnet branch from which to build the release', name: "MXNET_BRANCH")
    string(defaultValue: "", description: 'Optional commit id within specified branch to use for build', name: "MXNET_SHA")
    string(defaultValue: "Release Job", description: "Optional Job name", name: "RELEASE_JOB_NAME")
    string(defaultValue: "", description: "Job Type e.g. binary_release/static, python/docker, runtime_images, etc.", name: 'RELEASE_JOB_TYPE')
    string(defaultValue: "cpu,mkl,cu80,cu80mkl,cu90,cu90mkl,cu92,cu92mkl,cu100,cu100mkl", description: "Comma separated list of variants", name: "MXNET_VARIANTS")
    booleanParam(defaultValue: false, description: 'Whether this is a release build or not', name: "RELEASE_BUILD")
  }

  stages {
    stage("Job Information") {
      steps {
        echo """\
          |Job Name: ${params.RELEASE_JOB_NAME}
          |Job Type: ${params.RELEASE_JOB_TYPE}
          |Release Build: ${params.RELEASE_BUILD}
          |Branch: ${params.MXNET_BRANCH}
          |Commit Id: ${params.MXNET_SHA}
          |Variants: ${MXNET_VARIANTS}""".stripMargin()
      }
    }
    stage("Release Job") {
      steps {
        script {
          cd_utils = load('ci/cd/jenkins/Jenkinsfile_utils.groovy')
          utils = load('ci/Jenkinsfile_utils.groovy')
          utils.assign_node_labels(
            utility: 'restricted-utility',
            linux_cpu: 'restricted-mxnetlinux-cpu',
            linux_gpu: 'restricted-mxnetlinux-gpu',
            linux_gpu_p3: 'restricted-mxnetlinux-gpu-p3',
            windows_cpu: 'restricted-mxnetwindows-cpu',
            windows_gpu: 'restricted-mxnetwindows-gpu'
          )

          env.MXNET_SHA=cd_utils.get_mxnet_commit_id_for_build()

          sh "printenv"

          // Convert mxnet variants to a list
          def mxnet_variants = params.MXNET_VARIANTS.split(',').inject([]) { list, item ->
            list << item.trim()
          }

          // Load script for the supplied job type
          def custom_steps = load("ci/cd/${params.RELEASE_JOB_TYPE}/Jenkins_pipeline.groovy")

          // Extract the pipelines for the variants
          def pipelines = [:]
          
          for (variant in mxnet_variants) {
            pipelines << custom_steps.get_pipeline(variant)
          }

          // Execute them in parallel
          // The build result will be set to:
          //  - SUCCESS if all pipelines succeed
          //  - UNSTABLE if some (but not all) pipelines fail
          //  - FAILURE if all pipelines fail
          cd_utils.error_checked_parallel(pipelines)
        }
      }
    }
  }
}
