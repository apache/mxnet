/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

if (JavaVersion.current() < JavaVersion.VERSION_11) {
    apply plugin: "com.github.spotbugs"
    spotbugs {
        excludeFilter = file("${rootProject.projectDir}/tools/conf/findbugs-exclude.xml")
        ignoreFailures = false
        spotbugsTest.enabled = true
    }
    spotbugsMain {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
    spotbugsTest {
        reports {
            xml.enabled false
            html.enabled true
        }
    }
}

apply plugin: "pmd"
pmd {
    ignoreFailures = false
    pmdTest.enabled = false
    ruleSets = [] // workaround pmd gradle plugin bug
    ruleSetFiles = files("${rootProject.projectDir}/tools/conf/pmd.xml")
}
tasks.withType(Pmd){
    reports{
        xml.enabled=true
        html.enabled=true
    }
}

apply plugin: "checkstyle"
checkstyle {
    toolVersion = "8.26"
    ignoreFailures = false
    checkstyleTest.enabled = true
    configProperties = [
            "checkstyle.suppressions.file" : file("${rootProject.projectDir}/tools/conf/suppressions.xml"),
            "checkstyle.licenseHeader.file" : file("${rootProject.projectDir}/tools/conf/licenseHeader.java")
            ]
    configFile = file("${rootProject.projectDir}/tools/conf/checkstyle.xml")
}
checkstyleMain {
    classpath += configurations.compileClasspath
}
tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.5"
}
jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

test.finalizedBy jacocoTestReport
build.dependsOn javadoc
